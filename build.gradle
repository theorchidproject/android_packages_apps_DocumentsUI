// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        libs = [
                recyclerview          : 'androidx.recyclerview:recyclerview:1.2.1',
                recyclerview_selection: 'androidx.recyclerview:recyclerview-selection:1.2.0-alpha01',
                legacy_support_v4     : 'androidx.legacy:legacy-support-v4:1.0.0',
                appcompat             : 'androidx.appcompat:appcompat:1.4.0-alpha03',
                annotation            : 'androidx.annotation:annotation:1.1.0',
                mediarouter           : 'androidx.mediarouter:mediarouter:1.2.0-alpha01',
                material              : 'com.google.android.material:material:1.5.0-alpha02',
                transition            : 'androidx.transition:transition:1.2.0-qpreview01',
                compress              : 'org.apache.commons:commons-compress:1.21',
        ]
    }

    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.android.documentsui"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 30
        versionName "11"
    }

    signingConfigs {
        platform {
            storeFile file("platform.keystore")
            storePassword '123456'
            keyAlias 'platform'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.platform
        }

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.platform
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "DocumentsUI.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs = ['src', 'statslog-docsui-java-gen/gen']
            res.srcDirs = ['res']
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly files('libs/framework.jar')
    implementation files('libs/guava.jar')
    implementation(name: 'legacy-support-v13-1.1.0-alpha01', ext: 'aar')

    implementation libs.compress
    implementation libs.transition
    implementation libs.legacy_support_v4
    implementation libs.appcompat
    implementation libs.recyclerview
    implementation libs.recyclerview_selection
    implementation libs.material
}

allprojects {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            if (options.bootstrapClasspath != null) {
                Set<File> fileSet = options.bootstrapClasspath.getFiles()
                List<File> newFileList = new ArrayList<>();
                newFileList.add(new File("./libs/framework.jar"))
                newFileList.addAll(fileSet)
                options.bootstrapClasspath = files(
                        newFileList.toArray()
                )
            }
        }
    }
}